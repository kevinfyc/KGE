/*
* Viry3D
* Copyright 2014-2017 by Stack - stackos@qq.com
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*     http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

precision mediump float;

UniformTexture(1, 1) uniform sampler2D _Lightmap;
UniformTexture(0, 3) uniform sampler2D _Control;
UniformTexture(0, 5) uniform sampler2D _NHxRoughness;
UniformTexture(0, 6) uniform sampler2D _Splat0;
UniformTexture(0, 7) uniform sampler2D _Splat1;
UniformTexture(0, 8) uniform sampler2D _Splat2;
UniformTexture(0, 9) uniform sampler2D _Normal0;
UniformTexture(0, 10) uniform sampler2D _Normal1;
UniformTexture(0, 11) uniform sampler2D _Normal2;

UniformBuffer(0, 4) uniform buf_ps {
	vec4 _WorldSpaceLightPos;
	vec4 _LightColor;
	vec4 _WorldSpaceCameraPos;
	vec4 _Spec0;
	vec4 _Spec1;
	vec4 _Spec2;
	vec4 _Smoothness;
	vec4 _Color;
} u_buf;

Varying(0) in vec4 v_pack0;
Varying(1) in vec4 v_pack1;
Varying(2) in vec4 v_t2w0;
Varying(3) in vec4 v_t2w1;
Varying(4) in vec4 v_t2w2;
Varying(5) in vec2 v_uv2;

layout (location = 0) out vec4 o_frag;

vec2 pow4(vec2 x) { return x * x * x * x; }

void main() {
	vec2 uv_Control = v_pack0.xy;
	vec2 uv_Splat0 = v_pack0.zw;
	vec2 uv_Splat1 = v_pack1.xy;
	vec2 uv_Splat2 = v_pack1.zw;

	vec4 c;
	vec4 splat_control = texture(_Control, uv_Control);
	vec4 lay0 = texture(_Splat0, uv_Splat0);
	vec4 lay1 = texture(_Splat1, uv_Splat1);
	vec4 lay2 = texture(_Splat2, uv_Splat2);
	vec3 n0 = texture(_Normal0, uv_Splat0).rgb * 2.0 - 1.0;
	vec3 n1 = texture(_Normal1, uv_Splat1).rgb * 2.0 - 1.0;
	vec3 n2 = texture(_Normal2, uv_Splat2).rgb * 2.0 - 1.0;

	vec3 normal0 = normalize(vec3(dot(v_t2w0.xyz, n0), dot(v_t2w1.xyz, n0), dot(v_t2w2.xyz, n0)));
	vec3 normal1 = normalize(vec3(dot(v_t2w0.xyz, n1), dot(v_t2w1.xyz, n1), dot(v_t2w2.xyz, n1)));
	vec3 normal2 = normalize(vec3(dot(v_t2w0.xyz, n2), dot(v_t2w1.xyz, n2), dot(v_t2w2.xyz, n2)));

	c.a = 1.0;
	c.rgb = (lay0.rgb * splat_control.r + lay1.rgb * splat_control.g + lay2.rgb * splat_control.b + lay0.rgb * splat_control.a) * u_buf._Color.rgb;

	vec3 normal;
	normal = normalize(normal0 * splat_control.r + normal1 * splat_control.g + normal2 * splat_control.b + normal0 * splat_control.a);

	vec3 spec_blend;
	spec_blend = (lay0.a * u_buf._Spec0.rgb * splat_control.r + lay1.a * u_buf._Spec1.rgb * splat_control.g + lay2.a * u_buf._Spec2.rgb * splat_control.b + lay0.a * u_buf._Spec0.rgb * splat_control.a);

	float smoothness_blend;
	smoothness_blend = (u_buf._Smoothness.x * splat_control.r + u_buf._Smoothness.y * splat_control.g + u_buf._Smoothness.z * splat_control.b + u_buf._Smoothness.x * splat_control.a);

	vec4 lm = texture(_Lightmap, v_uv2) * 2.0;

	vec3 pos = vec3(v_t2w0.w, v_t2w1.w, v_t2w2.w);
	vec3 light_dir = normalize(u_buf._WorldSpaceLightPos.rgb - pos * u_buf._WorldSpaceLightPos.w);
	vec3 view_dir = normalize(u_buf._WorldSpaceCameraPos.rgb - pos);
	vec3 light_color = u_buf._LightColor.rgb;
	
	float nl = max(dot(normal, light_dir), 0.0);
	vec3 h = normalize(light_dir + view_dir);
	float nh = max(dot(normal, h), 0.0);
	float nv = max(dot(normal, view_dir), 0.0);
	vec3 ref = reflect(view_dir, normal);
	
	vec3 spec_color = spec_blend;
	float smoothness = smoothness_blend;
	float rlp4 = pow4(vec2(dot(ref, light_dir), 1.0 - nv)).x;
	float spec = texture(_NHxRoughness, vec2(rlp4, smoothness)).r * 16.0;
	
	vec3 lightmap = c.rgb * lm.rgb;
	vec3 diffuse = c.rgb * light_color * nl;
	vec3 specular = light_color * spec * spec_color;

	c.rgb = lightmap + diffuse + specular;

	o_frag = c;
}
